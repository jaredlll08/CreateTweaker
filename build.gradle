import com.blamejared.modtemplate.Utils

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://maven.blamejared.com' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'com.blamejared', name: 'ModTemplate', version: '2.+', changing: true
    }
}
plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}
apply plugin: 'com.blamejared.modtemplate'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'

group = 'com.blamejared.createtweaker'
version = Utils.updatingVersion('2.0.0')
archivesBaseName = 'CreateTweaker-1.18.2'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: "1.18.2"

    runs {
        client {
            workingDirectory project.file('run')

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                createtweaker {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            mods {
                createtweaker {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            args '--mod', 'createtweaker', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                createtweaker {
                    source sourceSets.main
                }
            }
        }
    }
}

modTemplate {
    mcVersion "1.18.2"
    curseHomepage "https://www.curseforge.com/minecraft/mc-mods/createtweaker"
    displayName "CreateTweaker"

    changelog {
        enabled true
        firstCommit "09445356925419c0111b0553a70f272a601f8bd5"
        repo "https://github.com/jaredlll08/CreateTweaker"
    }
    versionTracker {
        enabled true
        author "Jared"
        projectName "createtweaker"
    }
    webhook {
        enabled true
        curseId "437717"
        avatarUrl "https://media.forgecdn.net/avatars/333/541/637466462865206010.png"
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url "https://www.cursemaven.com"
    }
    maven {
        url "https://maven.blamejared.com"
    }
    maven {
        url "https://maven.tterrag.com"
    }
    maven {
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        name = "squiddev"
        url = "https://squiddev.cc/maven/"
        content {
            includeGroup "org.squiddev"
        }
    }
}

dependencies {
    def ctDep = 'com.blamejared.crafttweaker:CraftTweaker-forge-1.18.2:9.1.192'
    // Not actually sure if this even works, or if it is even required.
    def mcDep = 'net.minecraftforge:forge:1.18.2-40.2.4'

    minecraft mcDep
    implementation fg.deobf("com.jozufozu.flywheel:flywheel-forge-1.18.2:0.6.10-105")
    implementation fg.deobf("com.simibubi.create:create-1.18.2:0.5.1.e-318:all")
    compileOnly(ctDep)
    runtimeOnly(fg.deobf(ctDep))
    implementation fg.deobf("mezz.jei:jei-1.18.2:9.5.2.133")

    annotationProcessor 'com.blamejared.crafttweaker:Crafttweaker_Annotation_Processors-1.18.2:2.0.0.90'
    annotationProcessor 'com.google.code.gson:gson:2.8.6'
    annotationProcessor 'org.reflections:reflections:0.9.10'
    annotationProcessor mcDep
    annotationProcessor ctDep
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "createtweaker",
                "Specification-Vendor"    : "Jaredlll08",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : "Jaredlll08",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publish.dependsOn(project.tasks.getByName("assemble"))
publish.mustRunAfter(project.tasks.getByName("build"))

publishing {

    publications {

        mavenJava(MavenPublication) {

            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java

            // Allows the maven pom file to be modified.
            pom.withXml {

                // Go through all the dependencies.
                asNode().dependencies.dependency.each { dep ->

                    println 'Surpressing artifact ' + dep.artifactId.last().value().last() + ' from maven dependencies.'
                    assert dep.parent().remove(dep)
                }
            }
        }
    }

    repositories {

        maven {

            url "file://" + System.getenv("local_maven")
        }
    }

}

tasks.withType(GenerateModuleMetadata) {

    enabled = false
}

curseforge {

    apiKey = findProperty('curseforge_api_token') ?: 0
    project {
        id = "437717"
        releaseType = 'release'
        changelog = file("changelog.md")
        changelogType = 'markdown'
        addGameVersion("Forge")
        addGameVersion("1.18.2")
        mainArtifact(jar) {
            relations {
                requiredDependency "crafttweaker"
                requiredDependency "create"
            }
        }
    }

}
compileJava {
    options.compilerArgs << "-Acrafttweaker.processor.document.output_directory=${file('docsOut')}"
}