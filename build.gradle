buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://maven.blamejared.com' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath group: 'com.blamejared', name: 'ModTemplate', version: '1.+', changing: true
    }
}
plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}
apply plugin: 'com.blamejared.modtemplate'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'

group = 'com.blamejared.createtweaker'
version = '1.0.0'
archivesBaseName = 'CreateTweaker'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20200920-mixed-1.16.3'

    runs {
        client {
            workingDirectory project.file('run')
            jvmArgs "-Dmixin.env.disableRefMap=true"

            mods {
                createtweaker {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            jvmArgs "-Dmixin.env.disableRefMap=true"

            mods {
                createtweaker {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            jvmArgs "-Dmixin.env.disableRefMap=true"

            args '--mod', 'createtweaker', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                createtweaker {
                    source sourceSets.main
                }
            }
        }
    }
}

modTemplate {
    mcVersion "1.16.5"
    curseHomepage "https://www.curseforge.com/minecraft/mc-mods/createtweaker"
    displayName "CreateTweaker"

    changelog {
        enabled true
        firstCommit "567dc7c69260c3316651f718e2a182ab2ca836f9"
        repo "https://github.com/jaredlll08/CreateTweaker"
    }
    versionTracker {
        enabled true
        author "Jared"
        projectName "createtweaker"
    }
    webhook {
        enabled true
        curseId "437717"
        avatarUrl "https://media.forgecdn.net/avatars/333/541/637466462865206010.png"
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url "https://www.cursemaven.com"
    }
    maven {
        url "https://maven.blamejared.com"
    }
    maven {
        url "https://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.0.1'
    implementation fg.deobf("curse.maven:create-328085:3260165")
    compile fg.deobf("com.blamejared.crafttweaker:CraftTweaker-1.16.5:7.1.0.203")
    compile fg.deobf("com.blamejared.contenttweaker:ContentTweaker-1.16:1.0.0.develop.11")
    compile fg.deobf("mezz.jei:jei-1.16.4:7.6.1.65")
    runtimeOnly fg.deobf("curse.maven:openloader-354339:3060693")
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "createtweaker",
                "Specification-Vendor"    : "Jaredlll08",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : "Jaredlll08",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publish.dependsOn(project.tasks.getByName("assemble"))
publish.mustRunAfter(project.tasks.getByName("build"))

publishing {

    publications {

        mavenJava(MavenPublication) {

            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java

            // Allows the maven pom file to be modified.
            pom.withXml {

                // Go through all the dependencies.
                asNode().dependencies.dependency.each { dep ->

                    println 'Surpressing artifact ' + dep.artifactId.last().value().last() + ' from maven dependencies.'
                    assert dep.parent().remove(dep)
                }
            }
        }
    }

    repositories {

        maven {

            url "file://" + System.getenv("local_maven")
        }
    }

}

curseforge {

    apiKey = findProperty('curseforge_api_token') ?: 0
    project {
        id = "437717"
        releaseType = 'release'
        changelog = file("changelog.md")
        changelogType = 'markdown'
        mainArtifact(jar) {
            relations {
                requiredDependency "crafttweaker"
                requiredDependency "create"
            }
        }
    }

}